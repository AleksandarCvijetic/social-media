package resources.rules.feed

import java.time.LocalDateTime;

import com.example.social_media.entity.Post
import com.example.social_media.entity.Like
import com.example.social_media.entity.Hashtag
import com.example.social_media.service.FeedRequest


global FeedRequest feedRequest

rule "Base score for all posts"
salience 100
when
    $post : Post(
        user.id != feedRequest.user.id
    )
then
    feedRequest.addScore($post, 0);
end


rule "Recommend post with hashtag liked by user"
when
    $like : Like(
        userId == feedRequest.user.id,
        $likedPost : post
    )

    $h : Hashtag() from $likedPost.hashtags

    $candidatePost : Post(
        user.id != feedRequest.user.id,
        this != $likedPost 
    )
    Hashtag( name == $h.name) from $candidatePost.hashtags
then
    System.out.println("Nasao je lajkovani hesteg: " + $candidatePost.getId());
    feedRequest.addScore($candidatePost, 1);
end

rule "Recommend post with hashtag created by user"
when
    $candidatePost : Post(
        user.id != feedRequest.user.id
    )

    $h : Hashtag() from $candidatePost.hashtags

    $myPost : Post(
        user.id == feedRequest.user.id
    )
    Hashtag( name == $h.name ) from $myPost.hashtags

then
    System.out.println("CREATED HASHTAG MATCH: " + $candidatePost.getId());
    feedRequest.addScore($candidatePost, 1);
end

rule "Recommend post that is popular"
when

    $candidatePost : Post(
        user.id != feedRequest.user.id,
        createdAt >= LocalDateTime.now().minusHours(24)
    )

    $likeCount : Number(intValue > 10) from accumulate(
        Like(post == $candidatePost),
        count(1)
    )
then
    System.out.println("DA LI SI USAO ZA POPULARNON: " + $candidatePost.getId());
    feedRequest.addScore($candidatePost, 1); 
end

rule "Recommend post that uses popular hashtag"
when
    $candidatePost : Post(
        user.id != feedRequest.user.id
    )

    exists (
        $h : Hashtag() from $candidatePost.hashtags

        and Number(intValue > 5) from accumulate(
            Post(
                createdAt >= LocalDateTime.now().minusHours(24),
                hashtags contains $h
            ),
            count(1)
        )
    )
then
    System.out.println("POPULAR HASHTAG MATCH: " + $candidatePost.getId());
    feedRequest.addScore($candidatePost, 1);
end


